# -------------------------------------------------------- #
#       Алгоритм экспорта базы данных мониторинговой       #
#            системы The Dude компании MikroTik            #
# -------------------------------------------------------- #
#                                                          #
# Процедуры остановки, экспорта и запуска мониторинга      #
# могут занять значительное время в зависимости от         #
# размера базы и активности использования системы.         #
# После каждой процедуры выполняется цикл с предусловием   #
# для ожидания полного их завершения.                      #
#                                                          #
# -------------------------------------------------------- #

# Имя файла бекапа
:global name "dude-backup";

# Останавливаем службу мониторинга
/dude set enabled=no;
:while ([/dude get enabled]=true) do={
  :delay 1;
};

# Удаляем файл, сохранившийся от предыдущего
# бекапа, если таковой имеется
:if ([:len [/file find name=$name]]>0) do={
  /file remove $name;
};

# Выполняем экспорт базы данных в файл
/dude export-db backup-file=$name;
:while ([/dude get status]!="export done") do={
  :delay 1;
};

# Запускаем службу мониторинга
/dude set enabled=yes;
:while ([/dude get enabled]=false) do={
  :delay 1;
};

# Выводим уведомление об успешном экспорте в лог
:log info "Dude database export successfull";
